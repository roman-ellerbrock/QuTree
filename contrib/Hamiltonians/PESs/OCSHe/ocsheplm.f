C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C% FUNCTION PLM(L,MM,X)
C% Author: J. M. M. Howson and J. D. Hutson 2001
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      FUNCTION PLM(L,MM,X)
C
C     COMPUTES NORMALIZED ASSOC. LEGENDRE POLYNOMIALS BY RECURSION.
C     THE VALUES RETURNED ARE NORMALIZED FOR INTEGRATION OVER X
C     (I.E. INTEGRATION OVER COS THETA BUT NOT PHI).
C     NOTE THAT THE NORMALIZATION GIVES 
C           PLM(L,0,1)=SQRT(L+0.5)
C           PLM(L,0,X)=SQRT(L+0.5) P(L,X)
C     FOR M.NE.0, THE VALUE RETURNED DIFFERS FROM THE USUAL
C           DEFINITION OF THE ASSOCIATED LEGENDRE POLYNOMIAL
C           (E.G. EDMONDS PAGES 23-24) 
C           BY A FACTOR OF (-1)**M*SQRT(L+0.5)*SQRT((L-M)!/(L+M)!)
C     THUS THE SPHERICAL HARMONICS ARE 
C          CLM = PLM * EXP(I*M*PHI) / SQRT(L+0.5)
C          YLM = PLM * EXP(I*M*PHI) / SQRT(2*PI)
C
C     PROGRAM OF R. NERF MODIFED BY S. GREEN.
C       MOD FEB. 82 BY S.G. ACCORDING TO R.T PACK'S SUGGESTION
C       FOR IMPROVED ACCURACY LARGE ABS(X)
C     MODIFIED AUG 88 BY S.G. TO KEEP RAT FROM BLOWING UP AT LARGE L
C       BY INCLUDING FACTOR (-Z)**L IN PRECEDING LOOP
C     ERROR IN STMT. NO. 211 CORRECTED MAY 93 BY SG
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      M=IABS(MM)
      IF ( M.GT.L .OR. L.LT.0)  GO TO 9999
      XL=L
      XM=M
      P1=1.D0
      P2=X
      IF(L.EQ.0) GO TO 20
C *** USE ALTERNATE RECURSION FOR LARGE M OR LARGE ABS(X)
      IF (M.EQ.0)  GO TO 10
      XTEST=0.49999D0*(1.D0+1.D0/XM)
      MTEST=L/3
      IF (M.GT.MTEST .OR. ABS(X).GT.XTEST)  GO TO 210
C ***
   10 DO 100 I=1,L
      XI=I
      P3=((2.D0*XI+1.D0)*X*P2-XI*P1)/(XI+1.D0)
      P1=P2
  100 P2=P3
      IF (M.EQ.0) GO TO 20
C AT END OF LOOP P1=P(L,0,X)
      IF (ABS(X).GT.1.D0)  GO TO 9999
      Z=SQRT(1.D0-X*X)
      IF (Z.LE.1.D-10) GO TO 999
  201 P2=(XL+1.D0)*(P2-X*P1)/Z
      DO 200 I=1,M
      XI=I
      P3=-2.D0*X/Z*P2*XI-(XL+XI)*(XL-XI+1.D0)*P1
      P1=P2
  200 P2=P3
      GO TO 20
C ***
C     BELOW RECURS DOWN IN M, AS SUGGESTED BY R. T PACK
  210 IF (ABS(X).GT.1.D0)  GO TO 9999
      Z=SQRT(1.D0-X*X)
      IF (Z.LE.1.D-10)  GO TO 999
C     CALCULATE RATIO OF FACTORIALS FOR PLL
      RAT=1.D0
      XI=0.D0
      DO 211 I=1,L
      XI=XI+1.D0
  211 RAT=-0.5D0*Z*(XL+XI)*RAT
C     CALCULATE PLL   ----  N.B. ABOVE INCL (-Z)**L COMPUTATION
C     P1=RAT*(-Z)**L
      P1=RAT
      IF (M.EQ.L)  GO TO 20
      P2=P1
      P1=-X*P2/Z
      IF (M.EQ.L-1)  GO TO 20
      LM1=L-M-1
C     RECUR DOWNWARD IN M
      DO 213 I=1,LM1
      MU=L-I-1
      XMU=MU
      P3=P2
      P2=P1
      P1=2.D0*(XMU+1.D0)*X*P2/Z+P3
  213 P1=-P1/((XL-XMU)*(XL+XMU+1.D0))
      GO TO 20
C ***
C     NORMALIZATION . . .
   20 XNORM=(2.D0*XL+1.D0)/2.D0
      IF (M.LE.0)  GO TO 1000
      XLM=XL+1.D0
      XLP=XL
      DO 1100 I=1,M
      XLM=XLM-1.D0
      XLP=XLP+1.D0
 1100 XNORM=XNORM/(XLM*XLP)
 1000 PLM=P1*SQRT(XNORM)
      RETURN
 9999 WRITE(6,699)  L,MM,X
  699 FORMAT('0 * * * ERROR.  ARGUMENT OUT OF RANGE FOR PLM(',2I6,D16.8,
     1  ' ).')
C     IF Z=0, THEN X=1 AND PLM(1.)=0 FOR M.GT.0.
C       IN THAT CASE, WE HAVE BRANCHED TO 999
  999 PLM=0.D0
      RETURN
      END

